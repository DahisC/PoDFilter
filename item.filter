// =============================================================================================
//
// Synpoo's ultra strict PoD Filter
// v0.10
// Last updated: March 11 2021
// Discord: synpoo#7377
//
// This filter was built for the endgame ONLY and WILL hide majority of items
//
// =============================================================================================




// =============================================================
// BASIC ITEMS
//
// Hidden: All potions except supers and full rejuv, keys, scrolls
// =============================================================
ItemDisplay[GOLD<6000]:
ItemDisplay[leg]: %NAME%
ItemDisplay[hp1 OR hp2 OR hp3 OR hp4 OR mp1 OR mp2 OR mp3 OR mp4 OR rvs]:
ItemDisplay[gps OR ops OR gpm OR opm OR gpl OR opl]:
ItemDisplay[key OR vps OR yps OR wms OR tsc OR isc]:
ItemDisplay[hp5]: Heal
ItemDisplay[mp5]:	Mana
ItemDisplay[rvl]: Full

// =============================================================
// GEMS
//
// Hidden: All except flawless skulls and all perfects
// =============================================================
ItemDisplay[skl]: %NAME%
ItemDisplay[GEMLEVEL=1 OR GEMLEVEL=2 OR GEMLEVEL=3 OR GEMLEVEL=4]:

// =============================================================
// ENDGAME QUEST ITEMS AND POD ITEMS
//
// =============================================================
// Essences
ItemDisplay[tes]: %GRAY%  Essence%ORANGE% (Andariel and Duriel)  %NOTIFY-ORANGE%
ItemDisplay[ceh]: %GRAY%  Essence%ORANGE% (Mephisto)  %NOTIFY-ORANGE%
ItemDisplay[bet]: %GRAY%  Essence%ORANGE% (Diablo)  %NOTIFY-ORANGE%
ItemDisplay[fed]: %GRAY%  Essence%ORANGE% (Baal)  %NOTIFY-ORANGE%

// Keys and Organs
ItemDisplay[pk1]: %GRAY%  Key%ORANGE% (Countess)  %NOTIFY-ORANGE%
ItemDisplay[pk2]: %GRAY%  Key%ORANGE% (Summoner)  %NOTIFY-ORANGE%
ItemDisplay[pk3]: %GRAY%  Key%ORANGE% (Izual)  %NOTIFY-ORANGE%
ItemDisplay[dhn OR bey OR mbr]: %ORANGE%  %NAME%  %NOTIFY-ORANGE%

// PoD Items
ItemDisplay[cx5]: %RED%%NL%  %NAME%  %NL%%NOTIFY-RED%
ItemDisplay[cx7]: %NAME%%NOTIFY-ORANGE%
ItemDisplay[maz]: %RED%%NAME%%NOTIFY-RED%
ItemDisplay[(ma1 OR ma2 OR ma3 OR ma4 OR ma5 OR ma6 OR ma7 OR ma8 OR ma9 OR ma10) !ID]: %NL%*  %NAME%  *%NOTIFY-ORANGE%%NL%

// =============================================================
// RUNES, JEWELS, CHARMS
//
// Hidden: Runes below Lem
// =============================================================
ItemDisplay[RUNE>25]: %NL%%RED%*        %ORANGE%%NAME%        %RED%*%NL%%NOTIFY-ORANGE%
ItemDisplay[RUNE>19 AND RUNE<26]: %NL%%PURPLE%*   %ORANGE%%NAME%   %PURPLE%*%NL%%NOTIFY-ORANGE%
ItemDisplay[RUNE>0 AND RUNE<20]:

// Hidden: Unid Large charms, Grand charms <ilvl50, Small charms <ilvl50
ItemDisplay[cm1 OR cm3 (!ID AND ILVL>50)]: %NL%  %NAME% [i%ILVL%]  %NL%%NOTIFY-BLUE%
ItemDisplay[cm1 OR cm2 OR cm3 (!ID UNI)]: %NL%  %NAME%  %NL%%NOTIFY-GOLD%
ItemDisplay[(cm1 OR cm2 OR cm3) !ID]:

// Hidden: Unid Jewels <ilvl 45
ItemDisplay[jew MAG (!ID AND ILVL>45)]: %NAME% [i%ILVL%]
ItemDisplay[jew RARE (!ID AND ILVL>45)]: %NL%  %NAME% [i%ILVL%]  %NL%%NOTIFY-YELLOW%
ItemDisplay[jew UNI !ID]: %NL%*    %NAME%    *%NL%%NOTIFY-GOLD%
ItemDisplay[jew MAG OR RARE !ID]:

// =============================================================
// AMULETS, RINGS, BOLTS, ARROWS
//
// Hidden: (UNID) Magic amulets amulets/rings/quivers/arrows
// - Unid Set rings
// - Unid rare amulets/rings/arrows <ilvl74
// =============================================================
ItemDisplay[amu SET !ID]: %NL%%RED%o      %GREEN%%NAME%      %RED%o%NL%%NOTIFY-GREEN%
ItemDisplay[amu OR rin (UNI !ID)]: %NL%%RED%o     %GOLD%%NAME%     %RED%o%NL%%NOTIFY-GOLD%
ItemDisplay[amu OR rin OR aqv OR aq2 (!ID RARE AND ILVL>74)]: %YELLOW%o %NAME% [i%ILVL%] o
ItemDisplay[(amu OR rin OR aq2 OR cq2 OR aqv OR cqv) (NMAG OR MAG OR RARE) !ID]:
ItemDisplay[UNI (aq2 OR cq2 OR aqv OR cqv) !ID]:
ItemDisplay[rin SET !ID]:

// =============================================================
// WEAPONS
//
// =============================================================

// VENDOR
ItemDisplay[WP12 NMAG !ETH (wnd OR ywn OR 9wn) ILVL>49 (SK73>0 OR SK79=3 OR SK82=3 OR SK83>1 OR SK85=3 OR SK86>1 OR SK87>0 OR SK88>0 OR SK89>0 OR SK90>1 OR SK91>1 OR SK92>0 OR SK93>0 OR SK94>0 OR SK95>0)]: %ORANGE%$%PRICE%
ItemDisplay[WP12 NMAG !ETH SOCK=1 (SK73>0 OR SK79=3 OR SK82=3 OR SK83>1 OR SK85=3 OR SK86>1 OR SK87>0 OR SK88>0 OR SK89>0 OR SK90>1 OR SK91>1 OR SK92>0 OR SK93>0 OR SK94>0 OR SK95>0)]: %ORANGE%$%PRICE%
ItemDisplay[NMAG !ETH !ED=15 (7tk OR 7ta OR 7bk)]: %ORANGE%$%PRICE%

// UNIQUE
// titans, tstroke, windforce, death cleaver, executioners justice, bartucs
ItemDisplay[UNI (ama OR amf OR 6lw OR 7wa OR 7gi OR 9tw) !ID]: %NL%%RED%o  %GOLD%%NAME%  %RED%o%NL%%NOTIFY-GOLD%
// gull, wizspike, fleshripper, occy, eschutas, fathom
ItemDisplay[UNI (dgr OR 7dg OR 7kr OR oba OR obc OR obf) !ID]: %NL%%RED%o  %GOLD%%NAME%  %RED%o%NL%%NOTIFY-GOLD%
// reaper's toll, tomb reaver, astreons, ondals, mang song, azurewrath, lightsabre
ItemDisplay[UNI (7s8 OR 7pa OR 7ws OR 6cs OR 6ws OR 7cr) !ID]: %NL%%RED%o  %GOLD%%NAME%  %RED%o%NL%%NOTIFY-GOLD%
// grandfather, warshrike, lacerator, boneshade, deaths web
ItemDisplay[UNI (7gd OR 7bk OR 7b8 OR 7bw OR 7gw) !ID]: %NL%%RED%o  %GOLD%%NAME%  %RED%o%NL%%NOTIFY-GOLD%

// SET
// bk tribal guardian, nats wep, tals wep
ItemDisplay[SET (7wd OR 7qr OR oba) !ID]:  %NL%%RED%o  %GREEN%%NAME%  %RED%o%NL%%NOTIFY-GOLD%

// RUNEWORD BASES
// 4os GMB+3 skill
ItemDisplay[amc NMAG SOCK=4 TABSK0=3]: %NL%  %DARK_GREEN%%NAME% %RED%+3bow %SOCKETS%os  %NL%%NOTIFY-GRAY%
// 4os+ eth thresher/giant thresher/cryptic axe/mancatcher
ItemDisplay[(7s8 OR 7wc OR 7pa OR 7br) NMAG SOCK>3 ETH]: %NL%  %DARK_GREEN%%NAME% %RED%%SOCKETS%os ETH  %NL%%NOTIFY-GRAY%
// 5os+ zerker non eth
ItemDisplay[7wa NMAG SOCK>4 !ETH]: %NL%  %DARK_GREEN%%NAME% %RED%%SOCKETS%os  %NL%%NOTIFY-GRAY%
// 5os+ eth zerker, col blade, col sword
ItemDisplay[(7wa OR 7gd OR 7fb) NMAG SOCK>4 ETH]: %NL%  %DARK_GREEN%%NAME% %RED%%SOCKETS%os ETH  %NL%%NOTIFY-GRAY%
// 3os gtalon +2-3 light sentry
ItemDisplay[9tw NMAG SOCK=3 SK271>1]: %NL%  %DARK_GREEN%%NAME% %RED%+Sentry %SOCKETS%os  %NL%%NOTIFY-GRAY%

// hide all else
ItemDisplay[WEAPON (MAG OR RARE OR UNI OR SET) !ID]:
ItemDisplay[WEAPON NMAG !RW]:

// =============================================================
// ARMOR
//
// =============================================================

// UNIQUE
// arreats, jalal's, ravenlore, spirit keeper, kiras, griffs, tgods, arach, dungo
ItemDisplay[UNI (baa OR dra OR dre OR drd OR ci2 OR ci3 OR zhb OR ulc OR umc) !ID]: %NL%%RED%o  %GOLD%%NAME%  %RED%o%NL%%NOTIFY-GOLD%
// vmagi, shaft, skullders, que-hegan, arkaines, leviathan, gbane, tyrael
ItemDisplay[UNI (xea OR xhn OR xpl OR xtp OR upl OR uld OR utu OR uar) !ID]: %NL%%RED%o  %GOLD%%NAME%  %RED%o%NL%%NOTIFY-GOLD%
// waterwalks, silks, war trav, gore, treks, marrows, sdancer
ItemDisplay[UNI (xvb OR xmb OR xtb OR xhb OR uvb OR umb OR uhb) !ID]: %NL%%RED%o  %GOLD%%NAME%  %RED%o%NL%%NOTIFY-GOLD%
// chance, dracs, steelrends, stealskull, valk wing, vampgaze, hoz, stormshield, homunc, darkforce
ItemDisplay[UNI (mgl OR uvg OR uhg OR xlm OR xhm OR xh9 OR pa9 OR uit OR nea OR nef) !ID]: %NL%%RED%o  %GOLD%%NAME%  %RED%o%NL%%NOTIFY-GOLD%
// all elite helmets
ItemDisplay[UNI ELT EQ1 !ID]: %NL%%RED%o  %GOLD%%NAME%  %RED%o%NL%%NOTIFY-GOLD%
// 

// SET
// aldurs boots, ik chest, trang gloves, trang helm, trang belt, trang shield
ItemDisplay[SET (xtb OR uar OR xmg OR uh9 OR utc OR ne9 ) !ID]: %NL%%RED%o  %GREEN%%NAME%  %RED%o%NL%%NOTIFY-GREEN%
// tals chest, tals helm, tals boots, nats chest
ItemDisplay[SET (uth OR xsk OR zmb OR ucl) !ID]: %NL%%RED%o  %GREEN%%NAME%  %RED%o%NL%%NOTIFY-GREEN%

// RUNEWORD BASES
// 3os+ superior mage, dusk, archon
ItemDisplay[SUP (xtp OR uui OR utp) SOCK>2 !ETH]: %NL%  %DARK_GREEN%%NAME% %RED%%SOCKETS%os  %NL%%NOTIFY-GRAY%
// 3os+ eth elite armors (treachery, stone, duress)
ItemDisplay[NMAG ELT EQ2 SOCK>2 ETH]: %NL%  %DARK_GREEN%%NAME% %RED%%SOCKETS%os [ETH]  %NL%%NOTIFY-GRAY%
// 4os eth sacred targe, vortex shield (exile)
ItemDisplay[NMAG (pab OR paf) SOCK=4 ETH]: %NL%  %DARK_GREEN%%NAME% %RED%%SOCKETS%os [ETH]  %NL%%NOTIFY-GRAY%

// RARES >ilvl79
// pelts, barb helms, gloves, boots, belts, circs
ItemDisplay[RARE (EQ4 OR EQ5 OR EQ6 OR EQ7 OR CL1 OR CL2) ILVL>79]: %NAME% [i%ILVL%]


// hide all else
ItemDisplay[ARMOR (MAG OR RARE OR UNI OR SET) !ID]:
ItemDisplay[ARMOR NMAG !RW]: